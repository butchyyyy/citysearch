plugins {
    id 'org.springframework.boot' version '2.1.2.RELEASE'
    id 'org.hidetake.swagger.generator' version '2.16.0'
    id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.butchyyyy'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

ext {
    powermockVersion = '2.0.0'
    mockitoVersion = '2.24.0'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.swagger:swagger-annotations:1.5.18'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation files('libs/geonames-1.1.14.jar', 'libs/jdom-1.0.jar')
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.powermock:powermock-core:${powermockVersion}"
    testImplementation "org.powermock:powermock-module-junit4:${powermockVersion}"
    testImplementation "org.powermock:powermock-api-mockito2:${powermockVersion}"
    testImplementation 'io.rest-assured:rest-assured:3.3.0'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.4'
    swaggerUI 'org.webjars:swagger-ui:3.10.0'
}

swaggerSources {
    CitySearch {
        inputFile = file('src/main/resources/api/CitySearchApi.yaml')
        code {
            // OpenAPI 3.0.x validation not supported yet
            // dependsOn validation
            components = ['models', 'apis']
            language = 'spring'
            configFile = file('src/main/resources/api/GenConfig.json')
        }
    }
}

build.dependsOn generateSwaggerUI

compileJava.dependsOn swaggerSources.CitySearch.code
sourceSets.main.java.srcDir "${swaggerSources.CitySearch.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.CitySearch.code.outputDir}/src/main/resources"

bootJar {
    from('../frontend/dist') {
        into "static"
    }
    bootJar.dependsOn(':frontend:buildProd')
}